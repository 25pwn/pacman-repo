# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Cedric MATHIEU <me.xenom @ gmail.com>

_name=firefox
_channel=nightly
_lang=en-US
pkgname=${_name}-${_channel}-latest
pkgdesc=""
url="https://www.mozilla.org/"
pkgver=latest
pkgrel=1
arch=(i686 x86_64)
license=(MPL GPL LGPL)
depends=(dbus-glib gtk3 libxt nss mime-types)
optdepends=('pulseaudio: audio support'
            'ffmpeg: h.264 video'
            'hunspell: spell checking'
            'hyphen: hyphenation'
            'libnotify: notification integration'
            'networkmanager: location detection via available WiFi networks'
            'speech-dispatcher: text-to-speech')
_url="https://download-installer.cdn.mozilla.net/pub/${_name}/nightly/latest-mozilla-central"
_src="${_name}-${_version}.${_lang}.linux"
_filename="$(date -u +%Y%m%d)-${_src}"
source=("${_name}-${_channel}.desktop" 'policies.json' "${_name}-${_channel}::https://download.mozilla.org/?product=firefox-${_channel}-latest-ssl&os=linux64&lang=en-US")
sha256sums=('7660ade771b998d00047ff58a8b2edc6ad1afe07527fa4360b59197f0dec5f42'
            'e977c3d690224dd727d8761d2c0a66ba41bfd02ae0fd7850c4545d06b5a8e63c'
            'SKIP')
provides=("${_name}-${_channel}")
conflicts=("${_name}-${_channel}")
replaces=("firefox-nightly")

prepare() {
  echo $(cat ${_name}/application.ini | grep -m 1 "Version=")
  declare $(cat ${_name}/application.ini | grep -m 1 "Version=")
  echo ${Version}
  pkgver=${Version}
}

package() {
  OPT_PATH="opt/${_name}-${_channel}"

  # Install the package files
  install -d "${pkgdir}"/{usr/bin,opt}
  cp -r ${_name} "${pkgdir}"/${OPT_PATH}
  ln -s "/${OPT_PATH}/${_name}" "${pkgdir}"/usr/bin/${_name}-${_channel}

  # Install .desktop files
  install -Dm644 "${srcdir}"/${_name}-${_channel}.desktop -t "${pkgdir}"/usr/share/applications

  # Install icons
  SRC_LOC="${srcdir}"/${_name}/browser
  DEST_LOC="${pkgdir}"/usr/share/icons/hicolor
  for i in 16 32 48 64 128
  do
      install -Dm644 "${SRC_LOC}"/chrome/icons/default/default${i}.png "${DEST_LOC}"/${i}x${i}/apps/${_name}-${_channel}.png
  done

  # Disable auto-updates
  install -Dm644 "${srcdir}"/policies.json -t "${pkgdir}"/${OPT_PATH}/distribution

  # Use system-provided dictionaries
  rm -rf "${pkgdir}"/${OPT_PATH}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}"/${OPT_PATH}/dictionaries
  ln -sf /usr/share/hyphen "${pkgdir}"/${OPT_PATH}/hyphenation
}